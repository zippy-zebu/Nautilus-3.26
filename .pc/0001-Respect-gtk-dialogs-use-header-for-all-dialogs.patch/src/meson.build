built_sources = []
gdbus_sources = []
enums = []

gdbus_sources += gnome.gdbus_codegen ('nautilus-freedesktop-generated',
                                      join_paths (meson.source_root (), 'data', 'freedesktop-dbus-interfaces.xml'),
                                      interface_prefix: 'org.freedesktop',
                                      namespace: 'NautilusFreedesktop')
# The header is used in nautilus-desktop.
built_sources += gdbus_sources

gdbus_sources += gnome.gdbus_codegen ('nautilus-generated',
                                      join_paths (meson.source_root (), 'data', 'dbus-interfaces.xml'),
                                      interface_prefix: 'org.gnome.Nautilus',
                                      namespace: 'NautilusDBus')

gdbus_sources += gnome.gdbus_codegen ('nautilus-shell-search-provider-generated',
                                      join_paths (meson.source_root (), 'data', 'shell-search-provider-dbus-interfaces.xml'),
                                      interface_prefix: 'org.gnome',
                                      namespace: 'Nautilus')

enum_headers = [
    'nautilus-search-popover.h',
    'nautilus-special-location-bar.h',
    'nautilus-query.h',
    'nautilus-search-provider.h'
]

enums += gnome.mkenums ('nautilus-enum-types',
                        c_template: 'nautilus-enum-types.c.template',
                        h_template: 'nautilus-enum-types.h.template',
                        sources: enum_headers)

resources = gnome.compile_resources ('nautilus-resources',
                                     'resources/nautilus.gresource.xml',
                                     source_dir: 'resources',
                                     c_name: 'nautilus',
                                     extra_args: '--manual-register')
built_sources += resources[1]

libnautilus_sources = [
    enums,
    gdbus_sources,
    resources[0],
    'animation/egg-animation.c',
    'animation/egg-animation.h',
    'animation/egg-frame-source.c',
    'animation/egg-frame-source.h',
    'animation/ide-box-theatric.c',
    'animation/ide-box-theatric.h',
    'animation/ide-cairo.c',
    'animation/ide-cairo.h',
    'gtk/nautilusgtkplacesview.c',
    'gtk/nautilusgtkplacesviewprivate.h',
    'gtk/nautilusgtkplacesviewrow.c',
    'gtk/nautilusgtkplacesviewrowprivate.h',
    'nautilus-application.c',
    'nautilus-application.h',
    'nautilus-bookmark-list.c',
    'nautilus-bookmark-list.h',
    'nautilus-canvas-view.c',
    'nautilus-canvas-view.h',
    'nautilus-canvas-view-container.c',
    'nautilus-canvas-view-container.h',
    'nautilus-container-max-width.c',
    'nautilus-container-max-width.h',
    'nautilus-dbus-manager.c',
    'nautilus-dbus-manager.h',
    'nautilus-desktop-item-properties.c',
    'nautilus-desktop-item-properties.h',
    'nautilus-error-reporting.c',
    'nautilus-error-reporting.h',
    'nautilus-preferences-window.c',
    'nautilus-preferences-window.h',
    'nautilus-files-view.c',
    'nautilus-files-view.h',
    'nautilus-files-view-dnd.c',
    'nautilus-files-view-dnd.h',
    'nautilus-floating-bar.c',
    'nautilus-floating-bar.h',
    'nautilus-freedesktop-dbus.c',
    'nautilus-freedesktop-dbus.h',
    'nautilus-image-properties-page.c',
    'nautilus-image-properties-page.h',
    'nautilus-list-model.c',
    'nautilus-list-model.h',
    'nautilus-list-view.c',
    'nautilus-list-view.h',
    'nautilus-list-view-private.h',
    'nautilus-list-view-dnd.c',
    'nautilus-list-view-dnd.h',
    'nautilus-location-entry.c',
    'nautilus-location-entry.h',
    'nautilus-mime-actions.c',
    'nautilus-mime-actions.h',
    'nautilus-notebook.c',
    'nautilus-notebook.h',
    'nautilus-other-locations-window-slot.c',
    'nautilus-other-locations-window-slot.h',
    'nautilus-pathbar.c',
    'nautilus-pathbar.h',
    'nautilus-places-view.c',
    'nautilus-places-view.h',
    'nautilus-previewer.c',
    'nautilus-previewer.h',
    'nautilus-progress-info-widget.c',
    'nautilus-progress-info-widget.h',
    'nautilus-progress-persistence-handler.c',
    'nautilus-progress-persistence-handler.h',
    'nautilus-properties-window.c',
    'nautilus-properties-window.h',
    'nautilus-query-editor.c',
    'nautilus-query-editor.h',
    'nautilus-search-popover.c',
    'nautilus-self-check-functions.c',
    'nautilus-self-check-functions.h',
    'nautilus-shell-search-provider.c',
    'nautilus-special-location-bar.c',
    'nautilus-toolbar.c',
    'nautilus-toolbar.h',
    'nautilus-toolbar-menu-sections.h',
    'nautilus-trash-bar.c',
    'nautilus-trash-bar.h',
    'nautilus-view.c',
    'nautilus-view.h',
    'nautilus-view-icon-controller.c',
    'nautilus-view-icon-controller.h',
    'nautilus-view-icon-item-ui.c',
    'nautilus-view-icon-item-ui.h',
    'nautilus-view-icon-ui.c',
    'nautilus-view-icon-ui.h',
    'nautilus-view-item-model.c',
    'nautilus-view-item-model.h',
    'nautilus-view-model.c',
    'nautilus-view-model.h',
    'nautilus-window-slot.c',
    'nautilus-window-slot.h',
    'nautilus-window-slot-dnd.c',
    'nautilus-window-slot-dnd.h',
    'nautilus-window.c',
    'nautilus-window.h',
    'nautilus-x-content-bar.c',
    'nautilus-x-content-bar.h',
    'nautilus-bookmark.c',
    'nautilus-bookmark.h',
    'nautilus-canvas-container.c',
    'nautilus-canvas-container.h',
    'nautilus-canvas-dnd.c',
    'nautilus-canvas-dnd.h',
    'nautilus-canvas-item.c',
    'nautilus-canvas-item.h',
    'nautilus-canvas-private.h',
    'nautilus-clipboard.c',
    'nautilus-clipboard.h',
    'nautilus-column-chooser.c',
    'nautilus-column-chooser.h',
    'nautilus-column-utilities.c',
    'nautilus-column-utilities.h',
    'nautilus-debug.c',
    'nautilus-debug.h',
    'nautilus-directory-async.c',
    'nautilus-directory-notify.h',
    'nautilus-directory-private.h',
    'nautilus-directory.c',
    'nautilus-directory.h',
    'nautilus-dnd.c',
    'nautilus-dnd.h',
    'nautilus-file-attributes.h',
    'nautilus-file-changes-queue.c',
    'nautilus-file-changes-queue.h',
    'nautilus-file-conflict-dialog.c',
    'nautilus-file-conflict-dialog.h',
    'nautilus-file-name-widget-controller.c',
    'nautilus-file-name-widget-controller.h',
    'nautilus-rename-file-popover-controller.c',
    'nautilus-rename-file-popover-controller.h',
    'nautilus-new-folder-dialog-controller.c',
    'nautilus-new-folder-dialog-controller.h',
    'nautilus-compress-dialog-controller.c',
    'nautilus-compress-dialog-controller.h',
    'nautilus-operations-ui-manager.c',
    'nautilus-operations-ui-manager.h',
    'nautilus-file-operations.c',
    'nautilus-file-operations.h',
    'nautilus-file-private.h',
    'nautilus-file-queue.c',
    'nautilus-file-queue.h',
    'nautilus-file-utilities.c',
    'nautilus-file-utilities.h',
    'nautilus-file.c',
    'nautilus-file.h',
    'nautilus-global-preferences.c',
    'nautilus-global-preferences.h',
    'nautilus-icon-info.c',
    'nautilus-icon-info.h',
    'nautilus-icon-names.h',
    'nautilus-keyfile-metadata.c',
    'nautilus-keyfile-metadata.h',
    'nautilus-lib-self-check-functions.c',
    'nautilus-lib-self-check-functions.h',
    'nautilus-link.c',
    'nautilus-link.h',
    'nautilus-metadata.h',
    'nautilus-metadata.c',
    'nautilus-mime-application-chooser.c',
    'nautilus-mime-application-chooser.h',
    'nautilus-module.c',
    'nautilus-module.h',
    'nautilus-monitor.c',
    'nautilus-monitor.h',
    'nautilus-profile.c',
    'nautilus-profile.h',
    'nautilus-progress-info.c',
    'nautilus-progress-info.h',
    'nautilus-progress-info-manager.c',
    'nautilus-progress-info-manager.h',
    'nautilus-program-choosing.c',
    'nautilus-program-choosing.h',
    'nautilus-recent.c',
    'nautilus-recent.h',
    'nautilus-search-directory.c',
    'nautilus-search-directory.h',
    'nautilus-search-directory-file.c',
    'nautilus-search-directory-file.h',
    'nautilus-search-provider.c',
    'nautilus-search-provider.h',
    'nautilus-search-engine.c',
    'nautilus-search-engine.h',
    'nautilus-search-engine-model.c',
    'nautilus-search-engine-model.h',
    'nautilus-search-engine-simple.c',
    'nautilus-search-engine-simple.h',
    'nautilus-search-hit.c',
    'nautilus-search-hit.h',
    'nautilus-selection-canvas-item.c',
    'nautilus-selection-canvas-item.h',
    'nautilus-signaller.h',
    'nautilus-signaller.c',
    'nautilus-query.c',
    'nautilus-thumbnails.c',
    'nautilus-thumbnails.h',
    'nautilus-trash-monitor.c',
    'nautilus-trash-monitor.h',
    'nautilus-tree-view-drag-dest.c',
    'nautilus-tree-view-drag-dest.h',
    'nautilus-ui-utilities.c',
    'nautilus-ui-utilities.h',
    'nautilus-video-mime-types.h',
    'nautilus-vfs-directory.c',
    'nautilus-vfs-directory.h',
    'nautilus-vfs-file.c',
    'nautilus-vfs-file.h',
    'nautilus-file-undo-operations.c',
    'nautilus-file-undo-operations.h',
    'nautilus-file-undo-manager.c',
    'nautilus-file-undo-manager.h',
    'nautilus-batch-rename-dialog.c',
    'nautilus-batch-rename-dialog.h',
    'nautilus-batch-rename-utilities.c',
    'nautilus-batch-rename-utilities.h',
    'nautilus-search-engine-tracker.c',
    'nautilus-search-engine-tracker.h'
]

nautilus_deps = [glib,
                 gtk,
                 autoar,
                 xml,
                 gsettings_desktop_schemas,
                 libgd_dep,
                 gail,
                 gnome_desktop,
                 eel_2,
                 nautilus_extension,
                 x11,
                 gmodule_no_export,
                 tracker_sparql,
                 gio_unix]

if get_option ('enable-exif')
    nautilus_deps += exif
endif

if get_option ('enable-selinux')
    nautilus_deps += selinux
endif

if get_option ('enable-unity')
    nautilus_deps += [unity, dbusmenu]
    libnautilus_sources += [
        'unity-quicklist-handler.c',
        'unity-quicklist-handler.h',
        'unity-bookmarks-handler.c',
        'unity-bookmarks-handler.h'
    ]
endif

if get_option ('enable-xmp')
    nautilus_deps += exempi
endif

libnautilus = static_library ('nautilus',
                              libnautilus_sources,
                              dependencies: nautilus_deps,
                              include_directories: nautilus_include_dirs)

libnautilus_include_dirs = include_directories ('.')

libnautilus_dep = declare_dependency (link_with: libnautilus,
                                      include_directories: [nautilus_include_dirs,
                                                            libnautilus_include_dirs],
                                      dependencies: nautilus_deps,
                                      sources: built_sources)

nautilus = executable ('nautilus',
                       'nautilus-main.c',
                       dependencies: libnautilus_dep,
                       install: true)

test ('nautilus', nautilus, args: ['--check', '--g-fatal-warnings'])

nautilus_autorun_software = executable ('nautilus-autorun-software',
                                        'nautilus-autorun-software.c',
                                        dependencies: libnautilus_dep,
                                        install: true)
